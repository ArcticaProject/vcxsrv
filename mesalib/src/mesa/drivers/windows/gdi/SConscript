Import('*')

env = env.Clone()

env.Prepend(CPPPATH = [
    '#src/mapi',
    '#src/mesa',
])

env.AppendUnique(CPPDEFINES = [
    '_GDI32_', # prevent wgl* being declared __declspec(dllimport)
    'BUILD_GL32', # declare gl* as __declspec(dllexport) in Mesa headers 
])
if not env['gles']:
    # prevent _glapi_* from being declared __declspec(dllimport)
    env.Append(CPPDEFINES = ['_GLAPI_NO_EXPORTS'])

env.Prepend(LIBS = [
    glapi,
    mesa,
    glsl,
    'gdi32',
    'user32',
    'kernel32',
])

sources = [
    'mesa.def',
    'wgl.c',
    'wmesa.c',
]

mesagdi = env.SharedLibrary(
    target = 'opengl32',
    source = sources,
)

env.Alias('mesagdi', mesagdi)
